# CMake file for Barefoot P4 tools.

project(BF_P4TOOLS VERSION 0.1.0)

# Build options.
option(ENABLE_IWYU "Enable checking includes with IWYU" OFF)

# Add cmake directory to our module path. Also add cmake directory from p4c to
# inherit FindLibGc.
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")

# GSL-lite is needed for testgen.
add_subdirectory(submodules/gsl-lite EXCLUDE_FROM_ALL)

set(INJA_BUILD_TESTS OFF CACHE BOOL "Build unit tests when BUILD_TESTING is enabled.")
set(BUILD_BENCHMARK OFF CACHE BOOL "Build benchmark.")
add_subdirectory(submodules/inja)


# P4Tools does not support GMP
if(ENABLE_GMP)
  message(FATAL_ERROR "P4Tools does not support compilation with GMP. Please use -DENABLE_GMP=OFF")
endif()


# Import common definitions.
include(common)

# For files generated by the protobuf compiler
include_directories(BEFORE ${P4C_BINARY_DIR}/control-plane)

if(ENABLE_IWYU)
  message("Enabling IWYU checks.")
  find_program(iwyu_path NAMES include-what-you-use iwyu REQUIRED)
  set(
    iwyu_path
    ${iwyu_path}
    -Xiwyu
    --max_line_length=100
    -Xiwyu
    --no_fwd_decls
    -Xiwyu
    --mapping_file=${CMAKE_CURRENT_LIST_DIR}/scripts/tools/iwyu_mappings/p4c.imp
    -Xiwyu
    --mapping_file=${CMAKE_CURRENT_LIST_DIR}/scripts/tools/iwyu_mappings/boost-1.75-all.imp
    -Xiwyu
    --mapping_file=${CMAKE_CURRENT_LIST_DIR}/scripts/tools/iwyu_mappings/libcxx.imp
    -Xiwyu
    --mapping_file=${CMAKE_CURRENT_LIST_DIR}/scripts/tools/iwyu_mappings/gcc.libc.imp
    -Xiwyu
    --mapping_file=${CMAKE_CURRENT_LIST_DIR}/scripts/tools/iwyu_mappings/stl.c.headers.imp
  )
  message("IWYU command: ${iwyu_path}")
endif()


enable_testing()
set(ENABLE_TESTING ON)

# Include sub projects.
add_and_lint_subdirectory(${CMAKE_CURRENT_LIST_DIR}/common ${CMAKE_CURRENT_LIST_DIR}/scripts/tools)
add_and_lint_subdirectory(${CMAKE_CURRENT_LIST_DIR}/testgen ${CMAKE_CURRENT_LIST_DIR}/scripts/tools)
add_and_lint_subdirectory(${CMAKE_CURRENT_LIST_DIR}/p4check ${CMAKE_CURRENT_LIST_DIR}/scripts/tools)
add_and_lint_subdirectory(${CMAKE_CURRENT_LIST_DIR}/mutate ${CMAKE_CURRENT_LIST_DIR}/scripts/tools)
add_and_lint_subdirectory(${CMAKE_CURRENT_LIST_DIR}/smith ${CMAKE_CURRENT_LIST_DIR}/scripts/tools)

# Custom IR constructs for P4Testgen.
set(IR_DEF_FILES ${IR_DEF_FILES} ${CMAKE_CURRENT_SOURCE_DIR}/common/testgen.def PARENT_SCOPE)
